switch allow_GT;

//////////////////////////////////////////////////////////////////
// QualifiedName
//////////////////////////////////////////////////////////////////

_NameIdentifier
  ::= ID:name                       as NameIdentifier {kind = Normal}
  ;

_DtorIdentifier
  ::= "~" ID:name                   as NameIdentifier {kind = Dtor}
  ;

_TypeIdentifier
  ::= "enum" ID:name                as NameIdentifier {kind = Enum}
  ::= "enum" "class" ID:name        as NameIdentifier {kind = EnumClass}
  ::= "class" ID:name               as NameIdentifier {kind = Class}
  ::= "struct" ID:name              as NameIdentifier {kind = Struct}
  ::= "union" ID:name               as NameIdentifier {kind = Union}
  ;

_OperatorIdentifier
  ::= "operator" '""' ID:name       as NameIdentifier {kind = UserDefinedLiteral}
  ::= "operator" "new"              as OperatorIdentifier {op = New}
  ::= "operator" "new" "[" "]"      as OperatorIdentifier {op = NewArray}
  ::= "operator" "delete"           as OperatorIdentifier {op = Delete}
  ::= "operator" "delete" "[" "]"   as OperatorIdentifier {op = DeleteArray}
  ::= "operator" ","                as OperatorIdentifier {op = Comma}
  ::= "operator" "(" ")"            as OperatorIdentifier {op = Parantheses}
  ::= "operator" "[" "]"            as OperatorIdentifier {op = Bracket}
  ::= "operator" "->*"              as OperatorIdentifier {op = PointerDeref}
  ::= "operator" "->"               as OperatorIdentifier {op = Pointer}
  ::= "operator" "!="               as OperatorIdentifier {op = NE}
  ::= "operator" "!"                as OperatorIdentifier {op = Not}
  ::= "operator" "=="               as OperatorIdentifier {op = EQ}
  ::= "operator" "="                as OperatorIdentifier {op = Assign}
  ::= "operator" "~="               as OperatorIdentifier {op = RevertAssign}
  ::= "operator" "~"                as OperatorIdentifier {op = Revert}
  ::= "operator" "^="               as OperatorIdentifier {op = XorAssign}
  ::= "operator" "^"                as OperatorIdentifier {op = Xor}
  ::= "operator" "&&"               as OperatorIdentifier {op = And}
  ::= "operator" "&="               as OperatorIdentifier {op = AndAssign}
  ::= "operator" "&"                as OperatorIdentifier {op = BitwiseAnd}
  ::= "operator" "||"               as OperatorIdentifier {op = Or}
  ::= "operator" "|="               as OperatorIdentifier {op = OrAssign}
  ::= "operator" "|"                as OperatorIdentifier {op = BitwiseOr}
  ::= "operator" "*="               as OperatorIdentifier {op = MulAssign}
  ::= "operator" "*"                as OperatorIdentifier {op = Mul}
  ::= "operator" "/="               as OperatorIdentifier {op = DivAssign}
  ::= "operator" "/"                as OperatorIdentifier {op = Div}
  ::= "operator" "%="               as OperatorIdentifier {op = ModAssign}
  ::= "operator" "%"                as OperatorIdentifier {op = Mod}
  ::= "operator" "+="               as OperatorIdentifier {op = PlusAssign}
  ::= "operator" "++"               as OperatorIdentifier {op = Increase}
  ::= "operator" "+"                as OperatorIdentifier {op = Plus}
  ::= "operator" "-="               as OperatorIdentifier {op = MinusAssign}
  ::= "operator" "--"               as OperatorIdentifier {op = Decrease}
  ::= "operator" "-"                as OperatorIdentifier {op = Minus}
  ::= "operator" "<<="              as OperatorIdentifier {op = LeftShiftAssign}
  ::= "operator" "<<"               as OperatorIdentifier {op = LeftShift}
  ::= "operator" "<="               as OperatorIdentifier {op = LE}
  ::= "operator" "<"                as OperatorIdentifier {op = LT}
  ::= "operator" ">>="              as OperatorIdentifier {op = RightShiftAssign}
  ::= "operator" ">>"               as OperatorIdentifier {op = RightShift}
  ::= "operator" ">="               as OperatorIdentifier {op = GE}
  ::= "operator" ">"                as OperatorIdentifier {op = GT}
  ;

_Identifier
  ::= !_NameIdentifier
  ::= !_TypeIdentifier
  ::= !_OperatorIdentifier
  ;

_GenericArgument
  ::= _TypeOrExpr_NoComma_NoGT:argument ["...":variadic] as GenericArgument
  ;

_GenericArguments
  ::= "<" {_GenericArgument:arguments ; ","} ">" as GenericArguments
  ;

_QualifiedNameFragment
  ::= _Identifier:id [_GenericArguments:arguments] as partial QualifiedName
  ::= "template" _Identifier:id _GenericArguments:arguments as partial QualifiedName
  ;

_MemberQualifiedNameFragment
  ::= _DtorIdentifier:id as partial QualifiedName
  ;

_QualifiedName
  ::= _QualifiedNameFragment as QualifiedName {kind = Context}
  ::= "::" _QualifiedNameFragment as QualifiedName {kind = Root}
  ::= "auto" as QualifiedName {kind = Auto}
  ::= "decltype" "(" _Expr:expr ")" as QualifiedName {kind = Decltype}
  ::= _QualifiedName:parent "::" _QualifiedNameFragment as QualifiedName {kind = Member}
  ::= _QualifiedName:parent "::" _MemberQualifiedNameFragment as QualifiedName {kind = Member}
  ;

//////////////////////////////////////////////////////////////////
// PrimitiveExpr
//////////////////////////////////////////////////////////////////

_StringLiteralFragment
  ::= MACRO_LPREFIX as StringLiteralFragment {kind = Macro_LPREFIX}
  ::= STRING:literal as StringLiteralFragment {kind = String}
  ;

_PrimitiveExpr : TypeOrExpr
  ::= "true" as PrimitiveExprLiteral {kind = True}
  ::= "false" as PrimitiveExprLiteral {kind = False}
  ::= "nullptr" as PrimitiveExprLiteral {kind = Nullptr}
  ::= "this" as PrimitiveExprLiteral {kind = This}
  ::= "__nullptr" as PrimitiveExprLiteral {kind = __Nullptr}
  ::= INT:literal as NumericExprLiteral {kind = Integer}
  ::= HEX:literal as NumericExprLiteral {kind = Hex}
  ::= BIN:literal as NumericExprLiteral {kind = Binary}
  ::= FLOAT:literal as NumericExprLiteral {kind = Float}
  ::= FLOATHEX:literal as NumericExprLiteral {kind = FloatHex}
  ::= CHAR:literal as NumericExprLiteral {kind = Char}
  ::= _StringLiteralFragment:fragments {_StringLiteralFragment:fragments} as StringLiteral
  ::= "(" _Expr:expr ")" as ParenthesisExpr
  ::= "{" {_Expr_NoComma:arguments ; ","} "}" as BraceExpr
  ::= "dynamic_cast":keyword "<" _Type:type ">" "(" _Expr:expr ")" as CastExpr
  ::= "static_cast":keyword "<" _Type:type ">" "(" _Expr:expr ")" as CastExpr
  ::= "const_cast":keyword "<" _Type:type ">" "(" _Expr:expr ")" as CastExpr
  ::= "reinterpret_cast":keyword "<" _Type:type ">" "(" _Expr:expr ")" as CastExpr
  ::= "safe_cast":keyword "<" _Type:type ">" "(" _Expr:expr ")" as CastExpr
  ::= "typeid":keyword "(" _TypeOrExpr:argument ")" as SysFuncExpr
  ::= "alignof":keyword "(" _TypeOrExpr:argument ")" as SysFuncExpr
  ::= "__alignof":keyword "(" _TypeOrExpr:argument ")" as SysFuncExpr
  ::= "__uuidof":keyword "(" _TypeOrExpr:argument ")" as SysFuncExpr
  ::= "noexcept":keyword "(" _TypeOrExpr:argument ")" as SysFuncExpr
  ;

_PrimitiveExpr_QualifiedName
  ::= !_PrimitiveExpr
  ::= !_QualifiedName
  ;

//////////////////////////////////////////////////////////////////
// PostfixUnaryExpr
//////////////////////////////////////////////////////////////////

_PostfixUnaryCtorType
  ::= left_recursion_placeholder(Type)
  ::= !_Type
  ;

_PostfixUnaryCtor
  ::= _PostfixUnaryCtorType:operand "(" {_Expr_NoComma:arguments ; ","} ")" as CallExpr {kind = Parenthesis}
  ::= _PostfixUnaryCtorType:operand "{" {_Expr_NoComma:arguments ; ","} "}" as CallExpr {kind = Brace}
  ;

_PostfixUnaryExpr
  ::= left_recursion_placeholder(Shared,Expr)
  ::= !_PrimitiveExpr_QualifiedName
  ::= !_PostfixUnaryCtor
  ::= _PostfixUnaryExpr:left "." _MemberOperatorQualifiedName:right as BinaryExpr {op = Member}
  ::= _PostfixUnaryExpr:left "->" _MemberOperatorQualifiedName:right as BinaryExpr {op = Pointer}
  ::= _PostfixUnaryExpr:operand "++" as PostfixUnaryExpr {op = Increase}
  ::= _PostfixUnaryExpr:operand "--" as PostfixUnaryExpr {op = Decrease}
  ::= _PostfixUnaryExpr:operand "[" _Expr:index "]" as IndexExpr
  ::= _PostfixUnaryExpr:operand "(" {_Expr_NoComma:arguments ; ","} ")" as CallExpr {kind = Parenthesis}
  ;

//////////////////////////////////////////////////////////////////
// PrefixUnaryExpr
//////////////////////////////////////////////////////////////////

_NewInit
  ::= "[" _Expr:initArguments "]" {"[" _Expr:initArguments "]"} as partial NewExpr {init = Array}
  ::= "(" {_Expr_NoComma:initArguments ; ","} ")" as partial NewExpr {init = Parenthesis}
  ::= "{" {_Expr_NoComma:initArguments ; ","} "}" as partial NewExpr {init = Brace}
  ;

_NewFragment
  ::= ["(" {_Expr_NoComma:placementArguments ; ","} ")"] _QualifiedName:type [_NewInit] as partial NewExpr {init ?= None}
  ;

_DeleteFragment
  ::= _PrefixUnaryExpr:argument as partial DeleteExpr {array = NotArray}
  ::= "[" "]" _PrefixUnaryExpr:argument as partial DeleteExpr {array = Array}
  ;

_SizeofFragment
  ::= _PostfixUnaryExpr:argument as partial SizeofExpr
  ::= _AllPrefixUnaryExpr_NoCStyleCast:argument as partial SizeofExpr
  ::= "(" _Type:argument ")" as partial SizeofExpr
  ;

_AllPrefixUnaryExpr_NoCStyleCast
  ::= "++" _PrefixUnaryExpr:operand as PrefixUnaryExpr {op = Increase}
  ::= "--" _PrefixUnaryExpr:operand as PrefixUnaryExpr {op = Decrease}
  ::= "~" _PrefixUnaryExpr:operand as PrefixUnaryExpr {op = Revert}
  ::= "!" _PrefixUnaryExpr:operand as PrefixUnaryExpr {op = Not}
  ::= "-" _PrefixUnaryExpr:operand as PrefixUnaryExpr {op = Minus}
  ::= "+" _PrefixUnaryExpr:operand as PrefixUnaryExpr {op = Plus}
  ::= "&" _PrefixUnaryExpr:operand as PrefixUnaryExpr {op = BitwiseAnd}
  ::= "*" _PrefixUnaryExpr:operand as PrefixUnaryExpr {op = Mul}
  ::= "new" _NewFragment as NewExpr {scope = Context}
  ::= "::" "new" _NewFragment as NewExpr {scope = Root}
  ::= "delete" _DeleteFragment as DeleteExpr {scope = Context}
  ::= "::" "delete" _DeleteFragment as DeleteExpr {scope = Root}
  ::= "sizeof" ["...":variadic] _SizeofFragment as SizeofExpr
  ;

_AllPrefixUnaryExpr
  ::= !_AllPrefixUnaryExpr_NoCStyleCast
  ::= "(" _Type:type ")" _PrefixUnaryExpr:expr as CastExpr
  ;

_PrefixUnaryExpr
  ::= left_recursion_placeholder(PrefixUnaryExpr)
  ::= !_PostfixUnaryExpr
  ::= !_AllPrefixUnaryExpr
  ;

//////////////////////////////////////////////////////////////////
// BinaryExpr
//////////////////////////////////////////////////////////////////

_MemberOperatorIdentifier
  ::= !_NameIdentifier
  ::= !_DtorIdentifier
  ::= !_OperatorIdentifier
  ;

_MemberOperatorQualifiedName
  ::= _Identifier:id [_GenericArguments:arguments] as QualifiedName {kind = Context}
  ;

_BExpr0
  ::= !_PrefixUnaryExpr
  ::= _BExpr0:left ".*" _PrefixUnaryExpr:right as BinaryExpr {op = MemberDeref}
  ::= _BExpr0:left "->*" _PrefixUnaryExpr:right as BinaryExpr {op = PointerDeref}
  ;

_BExpr1
  ::= !_BExpr0
  ::= _BExpr1:left "*" _BExpr0:right as BinaryExpr {op = Mul}
  ::= _BExpr1:left "/" _BExpr0:right as BinaryExpr {op = Div}
  ::= _BExpr1:left "%" _BExpr0:right as BinaryExpr {op = Mod}
  ;

_BExpr2
  ::= !_BExpr1
  ::= _BExpr2:left "+" _BExpr1:right as BinaryExpr {op = Plus}
  ::= _BExpr2:left "-" _BExpr1:right as BinaryExpr {op = Minus}
  ;

_BExpr3
  ::= !_BExpr2
  ::= _BExpr3:left "<<" _BExpr2:right as BinaryExpr {op = LeftShift}
  ::= _BExpr3:left ">>" _BExpr2:right as BinaryExpr {op = RightShift}
  ;

_BExpr4
  ::= !_BExpr3
  ::= _BExpr4:left "<" _BExpr3:right as BinaryExpr {op = LT}
  ::= _BExpr4:left ?(allow_GT: ">" _BExpr3:right) as BinaryExpr {op = GT}
  ::= _BExpr4:left "<=" _BExpr3:right as BinaryExpr {op = LE}
  ::= _BExpr4:left ">=" _BExpr3:right as BinaryExpr {op = GE}
  ;

_BExpr5
  ::= !_BExpr4
  ::= _BExpr5:left "==" _BExpr4:right as BinaryExpr {op = EQ}
  ::= _BExpr5:left "!=" _BExpr4:right as BinaryExpr {op = NE}
  ;

_BExpr6
  ::= !_BExpr5
  ::= _BExpr6:left "&" _BExpr5:right as BinaryExpr {op = BitwiseAnd}
  ;

_BExpr7
  ::= !_BExpr6
  ::= _BExpr7:left "^" _BExpr6:right as BinaryExpr {op = Xor}
  ;

_BExpr8
  ::= !_BExpr7
  ::= _BExpr8:left "|" _BExpr7:right as BinaryExpr {op = BitwiseOr}
  ;

_BExpr9
  ::= !_BExpr8
  ::= _BExpr9:left "&&" _BExpr8:right as BinaryExpr {op = And}
  ;

_BExpr10
  ::= !_BExpr9
  ::= _BExpr10:left "||" _BExpr9:right as BinaryExpr {op = Or}
  ;

_ThrowExpr
  ::= "throw" [_BExpr10:argument] as ThrowExpr
  ;

_BExpr_NoComma
  ::= !_BExpr10
  ::= !_ThrowExpr
  ::= _BExpr10:condition "?" _Expr:trueBranch ":" _BExpr_NoComma:falseBranch as IfExpr
  ::= _BExpr10:left "=" _BExpr_NoComma:right as BinaryExpr {op = Assign}
  ::= _BExpr10:left "*=" _BExpr_NoComma:right as BinaryExpr {op = MulAssign}
  ::= _BExpr10:left "/=" _BExpr_NoComma:right as BinaryExpr {op = DivAssign}
  ::= _BExpr10:left "%=" _BExpr_NoComma:right as BinaryExpr {op = ModAssign}
  ::= _BExpr10:left "+=" _BExpr_NoComma:right as BinaryExpr {op = PlusAssign}
  ::= _BExpr10:left "-=" _BExpr_NoComma:right as BinaryExpr {op = MinusAssign}
  ::= _BExpr10:left "<<=" _BExpr_NoComma:right as BinaryExpr {op = LeftShiftAssign}
  ::= _BExpr10:left ">>=" _BExpr_NoComma:right as BinaryExpr {op = RightShiftAssign}
  ::= _BExpr10:left "&=" _BExpr_NoComma:right as BinaryExpr {op = AndAssign}
  ::= _BExpr10:left "|=" _BExpr_NoComma:right as BinaryExpr {op = OrAssign}
  ::= _BExpr10:left "^=" _BExpr_NoComma:right as BinaryExpr {op = XorAssign}
  ;

_BExpr
  ::= left_recursion_placeholder(ThrowExpr)
  ::= !_BExpr_NoComma
  ::= _BExpr:left "," _BExpr_NoComma:right as BinaryExpr {op = Comma}
  ;

//////////////////////////////////////////////////////////////////
// PrimitiveType
//////////////////////////////////////////////////////////////////

_PrimitiveTypeLiteral
  ::= "wchar_t":literal1                 as partial PrimitiveType
  ::= "char8_t":literal1                 as partial PrimitiveType
  ::= "char16_t":literal1                as partial PrimitiveType
  ::= "char32_t":literal1                as partial PrimitiveType
  ::= "char":literal1                    as partial PrimitiveType
  ::= "short":literal1                   as partial PrimitiveType
  ::= "int":literal1                     as partial PrimitiveType
  ::= "__int8":literal1                  as partial PrimitiveType
  ::= "__int16":literal1                 as partial PrimitiveType
  ::= "__int32":literal1                 as partial PrimitiveType
  ::= "__int64":literal1                 as partial PrimitiveType
  ::= "long":literal1                    as partial PrimitiveType
  ::= "long":literal1 "int":literal2     as partial PrimitiveType
  ::= "long":literal1 "long":literal2    as partial PrimitiveType
  ;

_PrimitiveType
  ::= "signed" [_PrimitiveTypeLiteral]   as PrimitiveType {kind = Signed}
  ::= "unsigned" [_PrimitiveTypeLiteral] as PrimitiveType {kind = Unsigned}
  ::= "void":literal1                    as PrimitiveType {kind = Neutral}
  ::= "bool":literal1                    as PrimitiveType {kind = Neutral}
  ::= "float":literal1                   as PrimitiveType {kind = Neutral}
  ::= "double":literal1                  as PrimitiveType {kind = Neutral}
  ::= "long":literal1 "double":literal2  as PrimitiveType {kind = Neutral}
  ::= _PrimitiveTypeLiteral              as PrimitiveType {kind = Neutral}
  ::= "typename" !_QualifiedName
  ;

_ShortType : TypeOrExpr
  ::= "const" _ShortType_QualifiedName:type as ConstType
  ::= "volatile" _ShortType_QualifiedName:type as VolatileType
  ::= !_PrimitiveType
  ;

_ShortType_QualifiedName
  ::= !_QualifiedName
  ::= !_ShortType
  ;

_LongType
  ::= left_recursion_placeholder(Shared,Type)
  ::= !_ShortType_QualifiedName
  ::= _LongType:type "const" as ConstType
  ::= _LongType:type "volatile" as VolatileType
  ;

//////////////////////////////////////////////////////////////////
// Declarator Components
//////////////////////////////////////////////////////////////////

_AdvancedTypeNoCVNoMember
  ::= "&" as AdvancedType {kind = LRef}
  ::= "&&" as AdvancedType {kind = RRef}
  ::= "*" as AdvancedType {kind = Pointer}
  ::= "*" "__ptr32" as AdvancedType {kind = Pointer32}
  ::= "*" "__ptr64" as AdvancedType {kind = Pointer64}
  ::= "alignas" "(" _Expr:argument ")" as AdvancedType {kind = AlignAs}
  ;

_AdvancedTypeCV
  ::= "const" as AdvancedType {kind = Const}
  ::= "volatile" as AdvancedType {kind = Volatile}
  ;

_AdvancedTypeNoMember
  ::= !_AdvancedTypeNoCVNoMember
  ::= !_AdvancedTypeCV
  ;

_AdvancedTypeMember
  ::= _QualifiedName:argument "::" as AdvancedType {kind = Member}
  ;

_DeclaratorKeyword
  ::= "constexpr":keyword as DeclaratorKeyword
  ::= "extern":keyword as DeclaratorKeyword
  ::= "static":keyword as DeclaratorKeyword
  ::= "mutable":keyword as DeclaratorKeyword
  ::= "thread_local":keyword as DeclaratorKeyword
  ::= "virtual":keyword as DeclaratorKeyword
  ::= "explicit":keyword as DeclaratorKeyword
  ::= "friend":keyword as DeclaratorKeyword
  ::= "abstract":keyword as DeclaratorKeyword
  ::= "inline":keyword as DeclaratorKeyword
  ::= "__inline":keyword as DeclaratorKeyword
  ::= "__forceinline":keyword as DeclaratorKeyword
  ::= "__cdecl":keyword as DeclaratorKeyword
  ::= "__clrcall":keyword as DeclaratorKeyword
  ::= "__stdcall":keyword as DeclaratorKeyword
  ::= "__fastcall":keyword as DeclaratorKeyword
  ::= "__thiscall":keyword as DeclaratorKeyword
  ::= "__vectorcall":keyword as DeclaratorKeyword
  ;

//================================================================

_DeclaratorAKFirst
  ::= _AdvancedTypeNoCVNoMember:advancedTypes as partial Declarator
  ::= _DeclaratorKeyword:keywords as partial Declarator
  ;

_DeclaratorAKFollow
  ::= _AdvancedTypeNoMember:advancedTypes as partial Declarator
  ::= _DeclaratorKeyword:keywords as partial Declarator
  ;

_DeclaratorAKCV
  ::= _AdvancedTypeCV:advancedTypes as partial Declarator
  ;

_DeclaratorAKMember
  ::= _AdvancedTypeMember:advancedTypes as partial Declarator
  ;

//================================================================

_DeclaratorFunctionParameter
  ::= {_DeclaratorKeyword:keywords} _LongType:type [_DeclaratorOptionalName:declarator] ["=" _Expr_NoComma:defaultValue] as FunctionParameter
  ;

_DeclaratorFunctionParameters
  ::= "...":variadic as partial DeclaratorFunctionPart
  ::= _DeclaratorFunctionParameter:parameters {"," _DeclaratorFunctionParameter:parameters} ["," "...":variadic] as partial DeclaratorFunctionPart
  ;

_FunctionKeyword
  ::= "constexpr":keyword as FunctionKeyword
  ::= "&":keyword as FunctionKeyword
  ::= "&&":keyword as FunctionKeyword
  ::= "const":keyword as FunctionKeyword
  ::= "volatile":keyword as FunctionKeyword
  ::= "override":keyword as FunctionKeyword
  ::= "final":keyword as FunctionKeyword
  ::= "mutable":keyword as FunctionKeyword
  ::= "noexcept":keyword ["(" {_Expr_NoComma:arguments ; ","} ")"] as FunctionKeyword
  ::= "throw":keyword ["(" {_Type:arguments ; ","} ")"] as FunctionKeyword
  ::= "=" '0':keyword as FunctionKeyword
  ::= "=" "delete":keyword as FunctionKeyword
  ::= "=" "default":keyword as FunctionKeyword
  ;

_DeclaratorFunctionPart
  ::= "(" [_DeclaratorFunctionParameters] ")" {_FunctionKeyword:keywords} ["->" _Type:deferredType] as DeclaratorFunctionPart
  ;

_DeclaratorArrayPart
  ::= "[" _Expr:argument "]" as DeclaratorArrayPart
  ;

_DeclaratorId
  ::= !_NameIdentifier
  ::= !_OperatorIdentifier
  ;

//////////////////////////////////////////////////////////////////
// Declarator
//////////////////////////////////////////////////////////////////

_DeclaratorBeforeInner
  ::= (_DeclaratorAKFirst [_DeclaratorAKMember] | _DeclaratorAKMember) {_DeclaratorAKFollow [_DeclaratorAKMember]} as partial Declarator
  ;

_DeclaratorAfterInner
  ::= _DeclaratorFunctionPart:funcPart | _DeclaratorArrayPart:arrayParts {_DeclaratorArrayPart:arrayParts} as partial Declarator
  ;

//================================================================

_InnerDeclaratorWithoutName
  ::= {_DeclaratorAKCV} !_DeclaratorWithoutName
  ;

_InnerDeclaratorInnerOptionalName
  ::= {_DeclaratorAKCV} !_DeclaratorOptionalName
  ;

_InnerDeclaratorInnerWithName
  ::= {_DeclaratorAKCV} !_DeclaratorWithName
  ;

//================================================================

_DeclaratorInnerWithoutName
  ::= "(" _InnerDeclaratorWithoutName:innerDeclarator ")" as partial Declarator
  ::= "...":variadic as partial Declarator
  ;

_DeclaratorInnerOptionalName
  ::= "(" _InnerDeclaratorInnerOptionalName:innerDeclarator ")" as partial Declarator
  ::=  _DeclaratorId:id as partial Declarator
  ::=  "...":variadic [_DeclaratorId:id] as partial Declarator
  ;

_DeclaratorInnerWithName
  ::= "(" _InnerDeclaratorInnerWithName:innerDeclarator ")" as partial Declarator
  ::= ["...":variadic] _DeclaratorId:id as partial Declarator
  ;

//================================================================

_DeclaratorWithoutName
  ::= _DeclaratorBeforeInner [_DeclaratorInnerWithoutName] [_DeclaratorAfterInner] as Declarator
  ::= _DeclaratorInnerWithoutName [_DeclaratorAfterInner] as Declarator
  ::= _DeclaratorAfterInner as Declarator
  ;

_DeclaratorOptionalName
  ::= _DeclaratorBeforeInner [_DeclaratorInnerOptionalName] [_DeclaratorAfterInner] as Declarator
  ::= _DeclaratorInnerOptionalName [_DeclaratorAfterInner] as Declarator
  ::= _DeclaratorAfterInner as Declarator
  ;

_DeclaratorWithName
  ::= [_DeclaratorBeforeInner] _DeclaratorInnerWithName [_DeclaratorAfterInner] as Declarator
  ;

//////////////////////////////////////////////////////////////////
// Type
//////////////////////////////////////////////////////////////////

_TypeOrDeclarator
  ::= !_LongType
  ::= _LongType:type _DeclaratorWithoutName:declarator as DeclaratorType
  ;

_TypeOnly
  ::= !_ShortType [left_recursion_inject(Type) _TypeOrDeclarator]
  ::= _DeclaratorKeyword:keywords {_DeclaratorKeyword:keywords} _LongType:type _DeclaratorWithoutName:declarator as DeclaratorType
  ;

_Type
  ::= !_QualifiedName [left_recursion_inject(Shared) _TypeOrDeclarator]
  ::= !_TypeOnly
  ;

//////////////////////////////////////////////////////////////////
// _Expr
//////////////////////////////////////////////////////////////////

_ExprOnly_NoComma
  ::= !_PrimitiveExpr [left_recursion_inject(Expr) _BExpr_NoComma]
  ::= !_AllPrefixUnaryExpr [left_recursion_inject(PrefixUnaryExpr) _BExpr_NoComma]
  ::= !_ThrowExpr
  ;

_Expr_NoComma_Raw
  ::= !_QualifiedName [left_recursion_inject(Shared) (_TypeOrDeclarator left_recursion_inject(type) _BExpr_NoComma) | _BExpr_NoComma]
  ::= !_ExprOnly_NoComma
  ::= !_TypeOnly left_recursion_inject(Type) _BExpr_NoComma
  ;

_ExprOnly
  ::= !_PrimitiveExpr [left_recursion_inject(Expr) _BExpr]
  ::= !_AllPrefixUnaryExpr left_recursion_inject(PrefixUnaryExpr) _BExpr
  ::= !_ThrowExpr [left_recursion_inject(ThrowExpr) _BExpr]
  ;

_Expr_Raw
  ::= !_QualifiedName [left_recursion_inject(Shared) (_TypeOrDeclarator left_recursion_inject(type) _BExpr) | _BExpr]
  ::= !_ExprOnly
  ::= !_TypeOnly left_recursion_inject(Type) _BExpr
  ;

//////////////////////////////////////////////////////////////////
// _TypeOrExpr
//////////////////////////////////////////////////////////////////

_TypeOrExpr_NoComma_Raw
  ::= !_QualifiedName [left_recursion_inject(Shared) (_TypeOrDeclarator [left_recursion_inject(type) _BExpr_NoComma]) | _BExpr_NoComma]
  ::= !_ExprOnly_NoComma
  ::= !_TypeOnly [left_recursion_inject(Type) _BExpr_NoComma]
  ;

_TypeOrExpr_Raw
  ::= !_QualifiedName [left_recursion_inject(Shared) (_TypeOrDeclarator [left_recursion_inject(type) _BExpr]) | _BExpr]
  ::= !_ExprOnly
  ::= !_TypeOnly [left_recursion_inject(Type) _BExpr]
  ;

//////////////////////////////////////////////////////////////////
// API
//////////////////////////////////////////////////////////////////

_Expr_NoComma            ::= !( allow_GT; !_Expr_NoComma_Raw);
_Expr                    ::= !( allow_GT; !_Expr_Raw);

_TypeOrExpr_NoComma_NoGT ::= !(!allow_GT; !_TypeOrExpr_NoComma_Raw);
_TypeOrExpr              ::= !( allow_GT; !_TypeOrExpr_Raw);

//////////////////////////////////////////////////////////////////
// SYNTAX
//////////////////////////////////////////////////////////////////

_File
  ::= ";" as File
  ;