//////////////////////////////////////////////////////////////////
// Class Body
//////////////////////////////////////////////////////////////////

_ClassInheritance
  ::=             _Type:type as ClassInheritance {accessor = Default}
  ::= "private"   _Type:type as ClassInheritance {accessor = Private}
  ::= "protected" _Type:type as ClassInheritance {accessor = Protected}
  ::= "public"    _Type:type as ClassInheritance {accessor = Public}
  ;

_ClassMemberPartPartial
  ::= _DeclWithSemicolon:decls {_DeclWithSemicolon:decls} as partial ClassMemberPart
  ;

_ClassMemberPart
  ::=                  _ClassMemberPartPartial  as ClassMemberPart {accessor = Default}
  ::= "private"   ":" [_ClassMemberPartPartial] as ClassMemberPart {accessor = Private}
  ::= "protected" ":" [_ClassMemberPartPartial] as ClassMemberPart {accessor = Protected}
  ::= "public"    ":" [_ClassMemberPartPartial] as ClassMemberPart {accessor = Public}
  ;

_ClassBody
  ::= ["::" {_ClassInheritance:inheritances ; ","}]
      "{" {_ClassMemberPart:memberParts} "}"
      {_DeclaratorVariablePartOptionalInit:varParts ","}
      as ClassBody
  ;

//////////////////////////////////////////////////////////////////
// Class Declarations
//////////////////////////////////////////////////////////////////

_ClassDeclPartial
  ::= _ClassBody:body as partial ClassDeclaration
  ::= ";" as partial ClassDeclaration
  ;

_ClassDeclInTypedef
  ::= "class"  [ID:name] _ClassBody:body as ClassDeclaration {kind = Class}
  ::= "struct" [ID:name] _ClassBody:body as ClassDeclaration {kind = Struct}
  ::= "union"  [ID:name] _ClassBody:body as ClassDeclaration {kind = Union}
  ;

_ClassDecl
  ::= "class"  ID:name _ClassDeclPartial as ClassDeclaration {kind = Class}
  ::= "struct" ID:name _ClassDeclPartial as ClassDeclaration {kind = Struct}
  ::= "union"  ID:name _ClassDeclPartial as ClassDeclaration {kind = Union}
  ;