//////////////////////////////////////////////////////////////////
// Class Body
//////////////////////////////////////////////////////////////////

_ClassInheritanceFirst
  ::= "::"             _Type:type as ClassInheritance {accessor = Default}
  ::= "::" "private"   _Type:type as ClassInheritance {accessor = Private}
  ::= "::" "protected" _Type:type as ClassInheritance {accessor = Protected}
  ::= "::" "public"    _Type:type as ClassInheritance {accessor = Public}
  ;

_ClassInheritanceSecond
  ::= ","             _Type:type as ClassInheritance {accessor = Default}
  ::= "," "private"   _Type:type as ClassInheritance {accessor = Private}
  ::= "," "protected" _Type:type as ClassInheritance {accessor = Protected}
  ::= "," "public"    _Type:type as ClassInheritance {accessor = Public}
  ;

_ClassMemberPart
  ::=  _DeclWithSemicolon:decls {_DeclWithSemicolon:decls} as ClassMemberPart {accessor = Default}
  ::= "private"   ":"           {_DeclWithSemicolon:decls} as ClassMemberPart {accessor = Private}
  ::= "protected" ":"           {_DeclWithSemicolon:decls} as ClassMemberPart {accessor = Protected}
  ::= "public"    ":"           {_DeclWithSemicolon:decls} as ClassMemberPart {accessor = Public}
  ;

_ClassBody
  ::= [_ClassInheritanceFirst:inheritances {_ClassInheritanceSecond:inheritances}]
      "{" {_ClassMemberPart:memberParts} "}"
      {_DeclaratorVariablePartOptionalInit:varParts ","}
      as ClassBody
  ;

//////////////////////////////////////////////////////////////////
// Class Declarations
//////////////////////////////////////////////////////////////////

_ClassDeclPartial
  ::= _ClassBody:body as partial ClassDeclaration
  ::= ";" as partial ClassDeclaration
  ;

_ClassDeclInTypedef
  ::= "class"  [ID:name] _ClassBody:body as ClassDeclaration {kind = Class}
  ::= "struct" [ID:name] _ClassBody:body as ClassDeclaration {kind = Struct}
  ::= "union"  [ID:name] _ClassBody:body as ClassDeclaration {kind = Union}
  ;

_ClassDecl
  ::= "class"  ID:name _ClassDeclPartial as ClassDeclaration {kind = Class}
  ::= "struct" ID:name _ClassDeclPartial as ClassDeclaration {kind = Struct}
  ::= "union"  ID:name _ClassDeclPartial as ClassDeclaration {kind = Union}
  ;