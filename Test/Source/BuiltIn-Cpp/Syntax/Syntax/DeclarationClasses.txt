//////////////////////////////////////////////////////////////////
// Class Body
//////////////////////////////////////////////////////////////////

_ClassInheritanceFirst
  ::= ":"             _Type:type as ClassInheritance {accessor = Default}
  ::= ":" "private"   _Type:type as ClassInheritance {accessor = Private}
  ::= ":" "protected" _Type:type as ClassInheritance {accessor = Protected}
  ::= ":" "public"    _Type:type as ClassInheritance {accessor = Public}
  ;

_ClassInheritanceSecond
  ::= ","             _Type:type as ClassInheritance {accessor = Default}
  ::= "," "private"   _Type:type as ClassInheritance {accessor = Private}
  ::= "," "protected" _Type:type as ClassInheritance {accessor = Protected}
  ::= "," "public"    _Type:type as ClassInheritance {accessor = Public}
  ;

_ClassMemberPartFirst
  ::=  _DeclWithSemicolon:decls {_DeclWithSemicolon:decls} as ClassMemberPart {accessor = Default}
  ;

_ClassMemberPartSecond
  ::= "private"   ":"           {_DeclWithSemicolon:decls} as ClassMemberPart {accessor = Private}
  ::= "protected" ":"           {_DeclWithSemicolon:decls} as ClassMemberPart {accessor = Protected}
  ::= "public"    ":"           {_DeclWithSemicolon:decls} as ClassMemberPart {accessor = Public}
  ;

_ClassBody
  ::= [_ClassInheritanceFirst:inheritances] {_ClassInheritanceSecond:inheritances}
      "{" [_ClassMemberPartFirst:memberParts] {_ClassMemberPartSecond:memberParts} "}"
      {_DeclaratorVariablePartOptionalInit:varParts ; ","}
      as ClassBody
  ;

//////////////////////////////////////////////////////////////////
// Class Declarations
//////////////////////////////////////////////////////////////////

_ClassDeclPrefix
  ::= ["friend":friendToken] "class"  as partial ClassDeclaration {kind = Class}
  ::= ["friend":friendToken] "struct" as partial ClassDeclaration {kind = Struct}
  ::= ["friend":friendToken] "union"  as partial ClassDeclaration {kind = Union}
  ;

_ClassDecl
  ::= _ClassDeclPrefix [ID:name] _ClassBody:body as ClassDeclaration
  ;

_ClassDeclForwardDecl
  ::= _ClassDeclPrefix ID:name as ClassDeclaration
  ;