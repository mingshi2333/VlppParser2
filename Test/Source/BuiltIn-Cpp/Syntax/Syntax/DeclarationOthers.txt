//////////////////////////////////////////////////////////////////
// Static Assert Declaration
//////////////////////////////////////////////////////////////////

_StaticAssertDecl
  ::= "static_assert" "(" _Expr:expr ["," _Expr:message] ")" as StaticAssertDeclaration
  ;
  
//////////////////////////////////////////////////////////////////
// Typedef Declaration
//////////////////////////////////////////////////////////////////

_TypedefCompatibleDecl
  ::= !_VarsDeclWithoutKeyword
  ::= !_ClassDeclInTypedef
  ;

_TypedefDecl
  ::= "typedef" _TypedefCompatibleDecl:decl as TypedefDeclaration
  ;

//////////////////////////////////////////////////////////////////
// Friend Declaration
//////////////////////////////////////////////////////////////////

_FriendDecl
  ::= "friend" _Decl:decl as FriendDeclaration
  ;
  
//////////////////////////////////////////////////////////////////
// Extern Declaration
//////////////////////////////////////////////////////////////////

_ExternDecl
  ::= "extern" [STRING] "{" _DeclWithSemicolon:decls "}" as ExternDeclaration
  ;
  
//////////////////////////////////////////////////////////////////
// Namespace Declaration
//////////////////////////////////////////////////////////////////

_NsName
  ::= ID:name as NamespaceName
  ;

_NsDecl
  ::= "namespace" {_NsName:names ; "::"} "{" _DeclWithSemicolon:decls "}" as NamespaceDeclaration
  ;

//////////////////////////////////////////////////////////////////
// Enum Declaration
//////////////////////////////////////////////////////////////////

_EnumItem
  ::= ID:name ["=" _Expr:expr] as EnumItem
  ;

_EnumBody
  ::= "{" [_EnumItem:items {_EnumItem:items ; ","} [","]] "}" as EnumBody
  ;

_EnumDeclPartial
  ::= _EnumBody:body as partial EnumDeclaration
  ::= ";" as partial EnumDeclaration
  ;

_EnumDecl
  ::= "enum" ID:name _EnumDeclPartial as EnumDeclaration {kind = Enum}
  ::= "enum" "class" ID:name [":" _Type:type] _EnumDeclPartial as EnumDeclaration {kind = EnumClass}
  ;

//////////////////////////////////////////////////////////////////
// Other Declarations
//////////////////////////////////////////////////////////////////

_OtherDecl
  ::= !_StaticAssertDecl
  ::= !_TypedefDecl
  ::= !_FriendDecl
  ::= !_ExternDecl
  ::= !_NsDecl
  ::= !_EnumDecl
  ;