//////////////////////////////////////////////////////////////////
// Declarator Before
//////////////////////////////////////////////////////////////////

_DeclaratorBeforeInner
  ::= (_DeclaratorAKFirst [_DeclaratorAKMember] | _DeclaratorAKMember) {_DeclaratorAKFollow [_DeclaratorAKMember]} as partial FuncVarDeclarator
  ;

//////////////////////////////////////////////////////////////////
// Declarator After
//////////////////////////////////////////////////////////////////

_DeclaratorAfterInner
  ::= _DeclaratorFunctionPart:funcPart | _DeclaratorArrayPart:arrayParts {_DeclaratorArrayPart:arrayParts} as partial FuncVarDeclarator
  ;

//////////////////////////////////////////////////////////////////
// Inner Declarator
//////////////////////////////////////////////////////////////////

_InnerDeclaratorWithoutName
  ::= {_DeclaratorAKCV} !_DeclaratorWithoutName
  ;

_InnerDeclaratorInnerOptionalName
  ::= {_DeclaratorAKCV} !_DeclaratorOptionalName
  ;

_InnerDeclaratorInnerRequiredName
  ::= {_DeclaratorAKCV} !_DeclaratorRequiredName
  ;

//////////////////////////////////////////////////////////////////
// Nesting Declarator
//////////////////////////////////////////////////////////////////

_DeclaratorInnerWithoutName
  ::= "(" _InnerDeclaratorWithoutName:innerDeclarator ")" as partial FuncVarDeclarator
  ;

_DeclaratorInnerOptionalName
  ::= "(" _InnerDeclaratorInnerOptionalName:innerDeclarator ")" as partial FuncVarDeclarator
  ::=  _DeclaratorId:id as partial FuncVarDeclarator
  ::=  "...":variadic [_DeclaratorId:id] as partial FuncVarDeclarator
  ;

_DeclaratorInnerRequiredName
  ::= "(" _InnerDeclaratorInnerRequiredName:innerDeclarator ")" as partial FuncVarDeclarator
  ::=  _DeclaratorId:id as partial FuncVarDeclarator
  ::=  "...":variadic _DeclaratorId:id as partial FuncVarDeclarator
  ;

//////////////////////////////////////////////////////////////////
// Declarator
//////////////////////////////////////////////////////////////////

_DeclaratorWithoutName
  ::= _DeclaratorBeforeInner [_DeclaratorInnerWithoutName] [_DeclaratorAfterInner] as FuncVarDeclarator
  ::= _DeclaratorInnerWithoutName [_DeclaratorAfterInner] as FuncVarDeclarator
  ::= _DeclaratorAfterInner as FuncVarDeclarator
  ;

_DeclaratorOptionalName
  ::= _DeclaratorBeforeInner [_DeclaratorInnerOptionalName] [_DeclaratorAfterInner] as FuncVarDeclarator
  ::= _DeclaratorInnerOptionalName [_DeclaratorAfterInner] as FuncVarDeclarator
  ::= _DeclaratorAfterInner as FuncVarDeclarator
  ;

_DeclaratorRequiredName
  ::= _DeclaratorBeforeInner _DeclaratorInnerRequiredName [_DeclaratorAfterInner] as FuncVarDeclarator
  ::= _DeclaratorInnerRequiredName [_DeclaratorAfterInner] as FuncVarDeclarator
  ;